plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.31'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'com.batchofcode'
version project.findProperty('buildNum') ?: '0.0.1_SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url  "https://kotlin.bintray.com/ktor" }
}

kotlin {
    macosX64("macos")
    linuxX64("linux")
}

dependencies {
    commonMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.2.1"
    commonMainImplementation "io.ktor:ktor-client-core:$ktor_version"

    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
    commonTestImplementation "io.ktor:ktor-client-mock:$ktor_version"
    commonTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.2.1"

    macosMainImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.2.1'
    macosMainImplementation "io.ktor:ktor-client-core-native:$ktor_version"
    macosMainImplementation "io.ktor:ktor-client-curl:$ktor_version"

    macosTestApi "io.ktor:ktor-client-mock-native:$ktor_version"
    macosTestApi 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.2.1'

    linuxMainImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.2.1'
    linuxMainImplementation "io.ktor:ktor-client-core-native:$ktor_version"
    linuxMainImplementation "io.ktor:ktor-client-curl:$ktor_version"

    linuxTestApi "io.ktor:ktor-client-mock-native:$ktor_version"
    linuxTestApi 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.2.1'
}


publishing {
    publications {
        kotlinMultiplatform {
            groupId = 'com.batchofcode'
            artifactId = 'lambda-runtime-kotlin-native'
            version = project.findProperty('buildNum') ?: '0.0.1_SNAPSHOT'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    override = true
//    publications = ['kotlinMultiplatform']
    filesSpec {
        from 'build/publications/LambdaRuntime/module.json'
        into 'com/batchofcode/lambda-runtime-kotlin-native/' + project.version
        rename 'module.json', 'lambda-runtime-kotlin-native-' + project.version + '.module'
    }
    pkg {
        repo = 'com.batchofcode'
        name = 'lambda-runtime-kotlin-native'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/c1phr/lambda-runtime-kotlin-native'
        version {
            name = project.findProperty('buildNum')
        }
    }
}

// TODO :kludge this is required for K/N publishing
bintrayUpload.dependsOn publishToMavenLocal

// This is for easier debugging of bintray uploading problems
bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it.name
    }
}

task runProgram {
    def buildType = 'debug' // Change to 'debug' to run application with debug symbols.
    dependsOn "build"
    doLast {
        def programFile = "build/bin/LambdaRuntime/main/${buildType}/executable/lambda-runtime-kotlin-native.kexe"
        exec {
            executable programFile
            args ''
        }
    }
}